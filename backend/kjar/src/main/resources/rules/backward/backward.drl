package backward;

import com.ftn.sbnz.model.BackwardModel;
import com.ftn.sbnz.model.models.Artist;
import com.ftn.sbnz.model.models.Album;
import com.ftn.sbnz.model.models.Song;
import java.util.Set;

global Set<String> facts;

query isConnected( String x, String y )
  BackwardModel( x, y; )
  or
  ( BackwardModel( z, y; ) and isConnected( x, z; ) )
end

rule "Basic backward rule"
when
    String( this == "basic-backward" )
    isConnected( x, y; )
then
    System.out.println(x + ", " + y);
end

rule "Find facts about artist"
when
    String( this == "artist-facts" )
    $artist: Artist($username: username)
    isConnected( $username, album; )
    isConnected( album, song; )
    isConnected( song, genre; )
then
    facts.add("Artist " + $username + " has album " + album);
    facts.add("Artist " + $username + " has song " + song);
    facts.add("Artist " + $username + " does genre " + genre);
end

rule "Find facts about album"
when
    String( this == "album-facts" )
    $album: Album($title: title)
    isConnected( artist, $title; )
    isConnected( $title, song; )
    isConnected( song, genre; )
then
    facts.add("Album " + $title + " is by artist " + artist);
    facts.add("Album " + $title + " has song " + song);
    facts.add("Album " + $title + " is by genre " + genre);
end

rule "Find facts about song"
when
    String( this == "song-facts" )
    song: Song($name: name)
    isConnected( artist, album; )
    isConnected( album, $name; )
    isConnected( $name, genre; )
then
    facts.add("Song " + $name + " is by artist " + artist);
    facts.add("Song " + $name + " is on album " + album);
    facts.add("Song " + $name + " is by genre " + genre);
end